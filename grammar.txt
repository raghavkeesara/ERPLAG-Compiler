<program> -> <moduleDeclarations><otherModules><driverModule><otherModules>
<moduleDeclarations> -> <moduleDeclaration><moduleDeclarations>
<moduleDeclarations> -> <EPSILON>
<moduleDeclaration> -> <DECLARE><MODULE><ID><SEMICOL>
<otherModules> -> <module><otherModules>
<otherModules> -> <EPSILON>
<driverModule> -> <DRIVERDEF><DRIVER><PROGRAM><DRIVERENDDEF><moduleDef>
<module> -> <DEF><MODULE><ID><ENDDEF><TAKES><INPUT><SQBO><input_plist><SQBC><SEMICOL><ret><moduleDef>
<ret> -> <RETURNS><SQBO><output_plist><SQBC><SEMICOL>
<ret> -> <EPSILON>
<input_plist> -> <ID><COLON><dataType><n1>
<n1> -> <COMMA><ID><COLON><dataType><n1>
<n1> -> <EPSILON>
<output_plist> -> <ID><COLON><type><n2>
<n2> -> <COMMA><ID><COLON><type><n2>
<n2> -> <EPSILON>
<dataType> -> <INTEGER>
<dataType> -> <REAL>
<dataType> -> <BOOLEAN>
<dataType> -> <ARRAY><SQBO><range><SQBC><OF><type>
<type> -> <INTEGER>
<type> -> <REAL>
<type> -> <BOOLEAN>
<moduleDef> -> <START><statements><END>
<statements> -> <statement><statements>
<statements> -> <EPSILON>
<statement> -> <ioStmt>
<statement> -> <simpleStmt>
<statement> -> <declareStmt>
<statement> -> <conditionalStmt>
<statement> -> <iterativeStmt>
<ioStmt> -> <GET_VALUE><BO><ID><BC><SEMICOL>
<ioStmt> -> <PRINT><BO><var><BC><SEMICOL>
<var> -> <ID><whichId>
<var> -> <NUM>
<var> -> <RNUM>
<whichId> -> <SQBO><ID><SQBC>
<whichId> -> <EPSILON>
<simpleStmt> -> <assignmentStmt>
<simpleStmt> -> <moduleReuseStmt>
<assignmentStmt> -> <ID><whichStmt>
<whichStmt> -> <lvalueIDStmt>
<whichStmt> -> <lvalueARRStmt>
<lvalueIDStmt> -> <ASSIGNOP><expression><SEMICOL>	
<lvalueARRStmt> -> <SQBO><index1><SQBC><ASSIGNOP><expression><SEMICOL>
<index1> -> <NUM>
<index1> -> <ID>
<moduleReuseStmt> -> <optional><USE><MODULE><ID><WITH><PARAMETERS><idList><SEMICOL>
<optional> -> <SQBO><idList><SQBC><ASSIGNOP>
<optional> -> <EPSILON>
<idList> -> <ID><n3>
<n3> -> <COMMA><ID><n3>
<n3> -> <EPSILON>
<expression> -> <arithmeticOrBooleanExpr>
<expression> -> <MINUS><BO><arithmeticExpr><BC>
<expression> -> <PLUS><BO><arithmeticExpr><BC>
<arithmeticOrBooleanExpr> -> <anyterm><n7>
<n7> -> <logicalOp><anyterm><n7>
<n7> -> <EPSILON>
<anyterm> -> <arithmeticExpr><n8>
<n8> -> <relationalOp><arithmeticExpr><n8> 
<n8> -> <EPSILON>
<arithmeticExpr> -> <term><n4>
<n4> -> <op1><term><n4>
<n4> -> <EPSILON>
<term> -> <factor><n5>
<n5> -> <op2><factor><n5>
<n5> -> <EPSILON>
<factor> -> <BO><arithmeticOrBooleanExpr><BC>
<factor> -> <var>
<op1> -> <PLUS>
<op1> -> <MINUS>
<op2> -> <MUL>
<op2> -> <DIV>
<logicalOp> -> <AND>
<logicalOp> -> <OR>
<relationalOp> -> <LT>
<relationalOp> -> <LE>
<relationalOp> -> <GT>
<relationalOp> -> <GE>
<relationalOp> -> <EQ>
<relationalOp> -> <NE>
<declareStmt> -> <DECLARE><idList><COLON><dataType><SEMICOL>
<conditionalStmt> -> <SWITCH><BO><ID><BC><START><caseStmts><default1><END>
<caseStmts> -> <CASE><value><COLON><statements><BREAK><SEMICOL><n9>
<n9> -> <CASE><value><COLON><statements><BREAK><SEMICOL><n9> 
<n9> -> <EPSILON>
<value> -> <NUM>
<value> -> <TRUEs> 
<value> -> <FALSEs>
<default1> -> <DEFAULT><COLON><statements><BREAK><SEMICOL> 
<default1> -> <EPSILON>
<iterativeStmt> -> <FOR><BO><ID><IN><range><BC><START><statements><END>
<iterativeStmt> -> <WHILE><BO><arithmeticOrBooleanExpr><BC><START><statements><END>	
<range> -> <NUM><RANGEOP><NUM>





